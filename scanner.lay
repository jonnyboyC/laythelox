import self.token_type;
import self.token:{Token};
import self.utils;

fn isDigit(c) {
  c >= '0' and c <= '9'
}

fn isAlpha(c) {
  (c >= 'a' and c <= 'z') or
  (c >= 'A' and c <= 'Z') or
  c == '_'
}

fn isAlphaNumeric(c) {
  isAlpha(c) or isDigit(c)
}

let keywords = {
  "and": token_type.AND,
  "class": token_type.CLASS,
  "else": token_type.ELSE,
  "false": token_type.FALSE,
  "for": token_type.FOR,
  "fun": token_type.FUN,
  "if": token_type.IF,
  "nil": token_type.NIL,
  "or": token_type.OR,
  "print": token_type.PRINT,
  "return": token_type.RETURN,
  "super": token_type.SUPER,
  "this": token_type.THIS,
  "true": token_type.TRUE,
  "var": token_type.VAR,
  "while": token_type.WHILE,
};

export class Scanner {
  init(source: string) {
    @source = source;
    @tokens = [];
    @start = 0;
    @current = 0;
    @line = 1;
  }

  scanTokens() {
    while !@isAtEnd() {
      @start = @current;
      @scanToken();
    }

    @tokens.push(Token(token_type.EOF, '', nil, @line));
    @tokens
  }

  scanToken() {

    let c = @advance();

    if c == '(' {
      @addToken(token_type.LEFT_PAREN);
    } else if c == ')' {
      @addToken(token_type.RIGHT_PAREN);
    } else if c == '{' {
      @addToken(token_type.LEFT_BRACE);
    } else if c == '}' {
      @addToken(token_type.RIGHT_BRACE);
    } else if c == ',' {
      @addToken(token_type.COMMA);
    } else if c == '.' {
      @addToken(token_type.DOT);
    } else if c == '-' {
      @addToken(token_type.MINUS);
    } else if c == '+' {
      @addToken(token_type.PLUS);
    } else if c == ';' {
      @addToken(token_type.SEMICOLON);
    } else if c == '*' {
      @addToken(token_type.STAR);
    } else if c == '!' {
      @addToken(@match('=') ? token_type.BANG_EQUAL : token_type.BANG);
    } else if c == '=' {
      @addToken(@match('=') ? token_type.EQUAL_EQUAL : token_type.EQUAL);
    } else if c == '<' {
      @addToken(@match('=') ? token_type.LESS_EQUAL : token_type.LESS);
    } else if c == '>' {
      @addToken(@match('=') ? token_type.GREATER_EQUAL : token_type.GREATER);
    } else if c == '/' {
      if @match('/') {
        while @peek() != '\n' and !@isAtEnd() {
          @advance();
        }
      } else {
        @addToken(token_type.SLASH);
      }
    } else if c == ' ' {
    } else if c == '\r' {
    } else if c == '\t' {
    } else if c == '\t' {
    } else if c == '\n' {
      @line += 1;
    } else if c == '"' {
      @string();
    } else {
      if isDigit(c) {
        @number();
      } else if isAlpha(c) {
        @identifer();
      } else {
        utils.error(@line, 'Unexpected character.');
      }
    }
  }

  identifer() {
    while isAlphaNumeric(@peek()) {
      @advance();
    }

    let text = @source.slice(@start, @current);
    let kind = keywords.get(text);

    if kind == nil {
      kind = token_type.IDENTIFIER;
    }

    @addToken(kind)
  }

  string() {
    while @peek() != '"' and !@isAtEnd() {
      if @peek() == '\n' { @line +=1; }
      @advance();
    }

    if @isAtEnd() {
      utils.error(@line, 'Unterminated string.');
      return;
    }

    @advance();

    // Trim the surrounding quotes.
    let value = @source.slice(@start + 1, @current - 1);
    @addTokenLiteral(token_type.STRING, value);
  }

  number() {
    while isDigit(@peek()) {
      @advance();
    }

    if @peek() == '.' and isDigit(@peekNext()) {
      @advance();

      while isDigit(@peek()) {
        @advance();
      }
    }

    @addTokenLiteral(token_type.NUMBER, Number.parse(@source.slice(@start, @current)));
  }

  advance() -> string {
    let c = @source[@current];
    @current += 1;
    c
  }

  peekNext() -> string {
    if @current + 1 >= @source.len() { return '\0'; }
    @source[@current + 1]
  }

  peek() -> string {
    if @isAtEnd() { return '\0'; }
    @source[@current]
  }

  addToken(kind: number) {
    @addTokenLiteral(kind, nil)
  }

  addTokenLiteral(kind: number, literal: any) {
    let text = @source.slice(@start, @current); // substring method
    @tokens.push(Token(kind, text, literal, @line));
  }

  match(expected: string) -> bool {
    if @isAtEnd() { return false; }
    if @source[@current] != expected { return false; }

    @current += 1;
    true
  }

  isAtEnd() -> bool {
    @current >= @source.len()
  }
}